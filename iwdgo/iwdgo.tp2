BACKUP ~iwdgo/backup~
AUTHOR ~Weigo~
VERSION ~v0.1 ALPHA~

//README ~iwdgo/readme/readme.html~

ALWAYS
	INCLUDE ~%MOD_FOLDER%/lib/a7_tools.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/macros.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/iwd_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/iwd_in_eet_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/iwd2_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/iwd2_in_eet_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/debug.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/a7_pvrz_tis.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/a7_area_references.tpa~

	ACTION_IF GAME_IS ~eet iwdee~ BEGIN
		OUTER_SPRINT ee ~ee~
	END

	ACTION_IF GAME_IS ~iwd how totlm iwd2~ BEGIN
		OUTER_SPRINT ee ~non-ee~
	END
	
	// remove bif directory
	ACTION_IF (DIRECTORY_EXISTS ~%MOD_FOLDER%/bif~) BEGIN
		ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
			AT_NOW ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
		END ELSE BEGIN //osx, unix
			AT_NOW ~rm -rf %MOD_FOLDER%/bif~ EXACT
		END
	END
	ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
		AT_EXIT ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
	END ELSE BEGIN //osx, unix
		AT_EXIT ~rm -rf %MOD_FOLDER%/bif~ EXACT
	END
	ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
		AT_UNINSTALL ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
	END ELSE BEGIN //osx, unix
		AT_UNINSTALL ~rm -rf %MOD_FOLDER%/bif~ EXACT
	END
	
	// remove old goidXXXX.bif or goiwXXXX.bif in data directory
	// prevents a long loading time, if a bif file should still be present in the data folder during installation
	// close the program (e.g. Near Infinity, weidu)
	ACTION_TRY
		ACTION_BASH_FOR ~data~ ~^go.*\.bif$~ BEGIN
			DELETE + ~%BASH_FOR_FILESPEC%~
		END
	WITH
		DEFAULT
			PRINT ~**********************************************************************************~
			PRINT ~The %BASH_FOR_FILESPEC% is still in use by a program. e.g.: Near Infinity or WeiDU%LNL%Please close the programs and try again~
			PRINT ~**********************************************************************************~
			ACTION_RERAISE
	END
	
END

/////////////////////////////
BEGIN ~IWDGO for IWD1_EET, IWD2_EET, IWDEE, IWD1, HOW, TOTLM, IWD2~
DESIGNATED 0 LABEL ~icewind_dale_graphical_overhaul~
REQUIRE_PREDICATE (GAME_IS ~iwd how totlm iwdee iwd2~ 
	OR MOD_IS_INSTALLED "setup-iwd1_eet.tp2" "1" 
	OR MOD_IS_INSTALLED "setup-iwd2_eet.tp2" "0" )
	~This mod is currently for IWD1_EET, IWD2_EET, IWD1, HOW, TOTLM, IWD2~

OUTER_SET iwd1_eet = 0
OUTER_SET iwd2_eet = 0
ACTION_IF MOD_IS_INSTALLED "setup-iwd1_eet.tp2" "1" BEGIN
	OUTER_SET iwd1_eet = 1
END
ACTION_IF MOD_IS_INSTALLED "setup-iwd2_eet.tp2" "0" BEGIN
	OUTER_SET iwd2_eet = 1
END

ACTION_DEFINE_ARRAY iwd1_areas BEGIN
	//outdoor
	ID1000 ID1100 ID1200 ID2000 ID2001 ID2100 ID3000 ID3600 ID4000 ID5000
	ID5101 ID5404 ID5502 ID6000  // ID7000 ID7005
	//indoor
	ID1002 ID1003 ID1004 ID1005 ID1006 ID1007 ID1008 ID1009 ID1010 ID1011
	ID1012 ID1013 ID1014 ID1015 ID1016 ID2101 ID2102 ID2103 ID2104 ID2105
	ID2106 ID2107 ID2108 ID2109 ID2110 ID2111 ID2112 ID2113 ID2114 ID2115 
	ID2116 ID3101 ID3201 ID3501 ID3601
END

ACTION_DEFINE_ARRAY iwd1_how BEGIN
	// outdoor
//	ID9100 ID9200 ID9300 ID9600
	
END

ACTION_DEFINE_ARRAY iwd1_totlm BEGIN
	// outdoor
//	ID9700 ID9709 ID9710
END

ACTION_DEFINE_ARRAY iwd2_areas BEGIN
	// outdoor
//	IW1000 IW1100 IW1200 IW2000 IW2001 IW2100 IW2101 IW2102 IW3000 IW3100 
//	IW4000 IW5000 IW5001 IW5004 IW5005 IW5010 IW5011 IW5012 IW5015 IW5016
//	IW5017 IW5020 IW5021 IW5024 IW5025 IW5026 IW5030 IW5200 IW5303 IW6000
//	IW6001 IW6002 IW6050 IW6100 IW6200 IW6201 IW6300 IW6400 IW6703 IW6800
//	
END

LAF ~INSTALL_MAIN~ 
	INT_VAR debug = 0  // list of all new used files
END




