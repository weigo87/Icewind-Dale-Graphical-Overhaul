
DEFINE_PATCH_FUNCTION Door_trigger_script_patch
	INT_VAR
		debug 				  = 1
		detail_debug		  = 0
		bggo_deleted		  = 0		
		travel_trigger_found  = 0	
		bggo_locked 		  = 0	// is door locked =1 unlocked =0
		dialog_string 		  = 0
	STR_VAR
		door_name_wed		= ""	// doorid from the wed file
		door_name			= ""
		new_trigger_name 	= ""    // name of the new travel trigger, does not overwrite existing
		door_script		 	= ""	// door script file
		trigger_script   	= ""	// trigger script file
		existing_door_name  = ""	
		trigger_name_dialog = ""
		area_name_dest		= ""
	RET
		bggo_deleted			//  0: existing door is used; 1: non working or not existing door, new one will be installed
		travel_trigger_found    // 0: install new one 1: use old one
		existing_door_name
		door_script
		trigger_script
		dialog_string
BEGIN
	SET found_door = 0
	
	// trigger and script set for modded doors
	// door check
	GET_OFFSET_ARRAY doors ARE_V10_DOORS
	PHP_EACH doors AS i => off BEGIN
		READ_ASCII off + 0x20 door_id
		READ_LONG off + 0x28 flag
		READ_ASCII off doorname (32) NULL
		PATCH_IF ~%door_name_wed%~ STR_CMP ~~ BEGIN
			PATCH_IF ~%door_id%~ STR_EQ ~%door_name_wed%~ BEGIN // found existing door with same doorid (wed file)
				SET found_door = 1
				
				// working and not working door
				PATCH_IF flag = 0 BEGIN //non working door - delete
					SET bggo_deleted = 1 // => install new one
					LPF fj_are_structure
						INT_VAR fj_delete_mode = i
						STR_VAR fj_structure_type = door
					END 
					PATCH_IF debug BEGIN PATCH_PRINT ~deleted non working "%doorname%"~ END
					PATCH_IF ~%door_script%~ STR_CMP ~~ AND !FILE_EXISTS ~%workspace%/bif/temp/%door_name%.baf~ BEGIN
						PATCH_IF debug BEGIN PATCH_PRINT ~deleted non working "%doorname%" - %door_script%~ END
						LPM Install_Script
					END
				END ELSE BEGIN // working door
					READ_ASCII off existing_door_name (32) NULL		// for the scripts
					SPRINT door_name ~%existing_door_name%~
					SET bggo_deleted = 0
					READ_ASCII off + 0x9c travel_trigger_name (32) NULL // this one we have to change
					// set or rename traveltrigger
					PATCH_IF detail_debug BEGIN PATCH_PRINT ~set or rename traveltrigger~ END
					PATCH_IF ~%travel_trigger_name%~ STR_EQ ~~ BEGIN	// if the travel trigger is missing
						WRITE_ASCIIE off + 0x9c ~%new_trigger_name%~
						SPRINT travel_trigger_name ~%new_trigger_name%~
						PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_wed: %doorname% - new traveltrigger: %new_trigger_name%~ END
					END ELSE BEGIN
						// doorname and triggername must have different names for the script triggers
						PATCH_IF ~%existing_door_name%~ STR_EQ ~%travel_trigger_name%~ BEGIN 
							SPRINT new_trigger_name ~bggo_%travel_trigger_name%~		// bggo_ to make it unique
							WRITE_ASCIIE off + 0x9c ~%new_trigger_name%~
							PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_wed: %doorname% - rename traveltrigger: %new_trigger_name%~ END
						END ELSE BEGIN 
							SPRINT new_trigger_name ~%travel_trigger_name%~
							PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_wed: %doorname% - different traveltrigger: %new_trigger_name%~ END
						END
					END
					
					// set or extend door script for existing doors
					PATCH_IF detail_debug BEGIN PATCH_PRINT ~set or extend door script for existing doors~ END
					PATCH_IF ~%door_script%~ STR_CMP ~~ AND !FILE_EXISTS ~%workspace%/bif/temp/%door_script%.baf~ BEGIN
						PATCH_IF debug BEGIN PATCH_PRINT ~set or extend "%doorname%" - %door_script%~ END
						LPM Install_Script
					END
					PATCH_IF ~%door_script%~ STR_CMP ~~ BEGIN
						READ_ASCII off + 0x80 door_scriptname
						PATCH_IF ~%door_scriptname%~ STR_EQ ~~ BEGIN
							WRITE_ASCIIE off + 0x80 ~%door_script%~
							INNER_ACTION BEGIN
								COMPILE ~%workspace%/bif/temp/%door_script%.baf~ EVAL
							END
							PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_script working %doorname% - new script: %door_script%.baf~ END
						END ELSE BEGIN
							INNER_ACTION BEGIN
								EXTEND_BOTTOM ~%door_scriptname%.bcs~ ~%workspace%/bif/temp/%door_script%.baf~ EVAL
							END
							PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_script working %doorname% - extend: %door_scriptname% with %door_script%.baf ~ END
						END
					END
				END
			END
		END
		
		// set or extend door script
		PATCH_IF detail_debug BEGIN PATCH_PRINT ~set or extend door script~ END
		PATCH_IF ~%door_name_wed%~ STR_EQ ~~ /*bggo_deleted = 1*/ AND ~%door_name%~ STR_CMP ~~ BEGIN
			PATCH_IF ~%door_name%~ STR_EQ ~%doorname%~ BEGIN
				found_door = 1
				SPRINT existing_door_name ~%doorname%~	// for the scripts
				
				// set new traveltrigger
				PATCH_IF ~%new_trigger_name%~ STR_CMP ~~ BEGIN
					WRITE_ASCIIE off + 0x9c ~%new_trigger_name%~ #32
					PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door: %doorname% - new traveltrigger: %new_trigger_name%~ END
				END
				
				// set or extend door script for existing doors
				PATCH_IF ~%door_script%~ STR_CMP ~~ BEGIN
					PATCH_IF !FILE_EXISTS ~%workspace%/bif/temp/%door_script%.baf~ BEGIN
						LPM Install_Script
					END
					READ_ASCII off + 0x80 door_scriptname
					PATCH_IF ~%door_scriptname%~ STR_EQ ~~ BEGIN
						WRITE_ASCIIE off + 0x80 ~%door_script%~
						PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_script: %door_script%~ END
						INNER_ACTION BEGIN
							ACTION_IF FILE_EXISTS_IN_GAME ~%door_script%.bcs~ BEGIN
								EXTEND_BOTTOM ~%door_scriptname%.bcs~ ~%workspace%/bif/temp/%door_script%.baf~ EVAL
							END ELSE BEGIN
								COMPILE ~%workspace%/bif/temp/%door_script%.baf~ EVAL
							END
						END
						PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_script working %doorname% - new script: %door_script%.baf~ END
					END ELSE BEGIN
						INNER_ACTION BEGIN
							EXTEND_BOTTOM ~%door_scriptname%.bcs~ ~%workspace%/bif/temp/%door_script%.baf~ EVAL
						END
						PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%door_script working new %doorname% - extend: %door_scriptname% with %door_script%.baf ~ END
					END
				END
			END
		END
	END
	PATCH_IF found_door = 0 BEGIN // not found an existing door
		bggo_deleted = 1 		  // => install new one
		PATCH_IF ~%door_script%~ STR_CMP ~~ AND !FILE_EXISTS ~%workspace%/bif/temp/%door_script%.baf~ BEGIN
			PATCH_IF debug BEGIN PATCH_PRINT ~new "%door_name%" - %door_script%~ END
			LPM Install_Script
		END
	END
	
	PATCH_IF bggo_deleted = 1 AND ~%door_script%~ STR_CMP ~~ BEGIN
		PATCH_IF debug BEGIN PATCH_PRINT ~deleted "%door_name_wed%" - %door_script%~ END
		INNER_ACTION BEGIN
			COMPILE ~%workspace%/bif/temp/%door_script%.baf~ EVAL
		END
	END
				
	// rename trigger and set trigger script
	PATCH_IF detail_debug BEGIN PATCH_PRINT ~search for existing travel_trigger~ END
	GET_OFFSET_ARRAY triggers ARE_V10_REGIONS
	PHP_EACH triggers AS i => off BEGIN
		READ_ASCII off triggername (32) NULL
		// search for existing travel_trigger
		PATCH_IF ~%triggername%~ STR_EQ ~%door_name%~ OR ~%triggername%~ STR_EQ ~%new_trigger_name%~ BEGIN
			SET travel_trigger_found = 1
			PATCH_IF ~%new_trigger_name%~ STR_CMP ~~ BEGIN
				WRITE_ASCIIE off ~%new_trigger_name%~ #32
				PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%trigger: %triggername% - new traveltrigger: %new_trigger_name%~ END
			END ELSE BEGIN
				SPRINT new_trigger_name ~bggo_%triggername%~		// bggo_ to make it unique
				WRITE_ASCIIE off ~%new_trigger_name%~ #32
				PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%trigger: %triggername% - rename traveltrigger: %new_trigger_name%~ END
			END
			WRITE_LONG off + 0x60 THIS | BIT11
			PATCH_IF ~%trigger_script%~ STR_CMP ~~ BEGIN
				READ_ASCII off + 0x7c trigger_scriptname
				PATCH_IF ~%trigger_scriptname%~ STR_EQ ~~ BEGIN
					WRITE_ASCIIE off + 0x7c ~%trigger_script%~
					INNER_ACTION BEGIN
						COMPILE ~%workspace%/bif/temp/%trigger_script%.baf~ EVAL 
					END
					PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%%triggername% - new script: %trigger_script%.baf~ END
				END ELSE BEGIN
					INNER_ACTION BEGIN
						EXTEND_BOTTOM ~%trigger_scriptname%.bcs~ ~%workspace%/bif/temp/%trigger_script%.baf~ EVAL
					END
					PATCH_IF debug BEGIN PATCH_PRINT ~%TAB%%triggername% - extend: %trigger_scriptname% with %trigger_script%.baf ~ END
				END
			END
		END
	END
	// if there is no existing trigger, we have to compile the trigger script
	PATCH_IF ~%trigger_script%~ STR_CMP ~~ BEGIN
		PATCH_IF !FILE_EXISTS_IN_GAME ~%trigger_script%.bcs~ BEGIN
			INNER_ACTION BEGIN
				COMPILE ~%workspace%/bif/temp/%trigger_script%.baf~ EVAL 
			END
		END ELSE BEGIN
			INNER_ACTION BEGIN
				EXTEND_BOTTOM ~%trigger_script%.bcs~ ~%workspace%/bif/temp/%trigger_script%.baf~ EVAL
			END
		END
	END
	PATCH_IF ~%existing_door_name%~ STR_EQ ~~ BEGIN
		SPRINT existing_door_name ~%door_name%~
	END
END


DEFINE_PATCH_FUNCTION CreateAreaTravelScript 
	INT_VAR
		bggo_locked 		  = 0	// is door locked =1 unlocked =0
		dialog_string 		  = 0
	STR_VAR
		door_script 	      = ~~
		area_name        	  = ~~
		door_name		 	  = ~~
		trigger_name_dialog   = ~~
		area_name_dest		  = ~~
		old_door_name 		  = ~~ // doorname from an existing door (perhabs from another mod)
				// if the variable is empty the door name is used otherwise the old name is used
		RET
		door_script
		dialog_string
BEGIN
	PATCH_IF ~%old_door_name%~ STR_CMP ~~ BEGIN
		SPRINT door_name ~%old_door_name%~
	END
	// changes for scripts with infotriggers
	PATCH_IF ~%trigger_name_dialog%~ STR_CMP ~~ BEGIN
		GET_OFFSET_ARRAY triggers ARE_V10_REGIONS
		PHP_EACH triggers AS i => off BEGIN
			READ_ASCII off triggername (32) NULL
			PATCH_IF ~%triggername%~ STR_EQ ~%trigger_name_dialog%~ BEGIN
				READ_LONG off + 0x64 dialog_string
			END
		END
		SPRINT door_click
		~IF
			Clicked([ANYONE])
			OpenState("%door_name%",FALSE)
			Global("%area_name%_text_%door_name%","%area_name%",0)
			Global("BGGO_unlocked_door_%area_name_dest%","GLOBAL",0)
		THEN
			RESPONSE #100
				DisplayString("%door_name%",%dialog_string%)
		END~
		SPRINT door_click_unlock ~SetGlobal("%area_name%_text_%door_name%","%area_name%",1)~
	END ELSE BEGIN
		SPRINT door_click ~~
		SPRINT door_click_unlock ~~
	END

	// changes in script for locked doors
	PATCH_IF bggo_locked = 1 BEGIN
		SPRINT set_unlock_door 
		~IF
			Unlocked([ANYONE])
			Global("BGGO_unlocked_door_%area_name_dest%","GLOBAL",0)
		THEN
			RESPONSE #100
				SetGlobal("BGGO_unlocked_door_%area_name_dest%","GLOBAL",1)
				%door_click_unlock%
		END~
		SPRINT global_unlock_door 
		~IF
			Global("BGGO_unlocked_door_%area_name_dest%","GLOBAL",1)
		THEN
			RESPONSE #100
				Unlock("%door_name%")
				%door_click_unlock%
				Continue()
		END~
		SPRINT global_unlock_door_trigger ~Global("BGGO_unlocked_door_%area_name_dest%","GLOBAL",1)~
	END ELSE BEGIN
		SPRINT set_unlock_door ~~
		SPRINT global_unlock_door ~~
		SPRINT global_unlock_door_trigger ~~
	END
	
	INNER_ACTION BEGIN
		EXTEND_BOTTOM ~%are2%.bcs~ ~.../%MOD_FOLDER%/inlined_door_open_close.baf~ EVAL
		COPY ~.../%MOD_FOLDER%/inlined_door_script.baf~ ~%workspace%/bif/temp/%door_script%.baf~ EVAL
//		COMPILE ~%workspace%/bif/temp/%door_script%.baf~ EVAL
	END
END

DEFINE_PATCH_MACRO Install_Script
BEGIN
	LPF CreateAreaTravelScript
	INT_VAR
		bggo_locked 		  = ~%bggo_locked%~	// is door locked =1 unlocked =0
	STR_VAR
		door_script 	      = EVAL ~%door_script%~ 	    
		area_name        	  = EVAL ~%area_name%~     	
		door_name		 	  = EVAL ~%door_name%~	 	
		trigger_name_dialog   = EVAL ~%trigger_name_dialog%~
		old_door_name 		  = EVAL ~%existing_door_name%~ 
		area_name_dest		  = EVAL ~%area_name_dest%~
		// doorname from an existing door (perhabs from another mod)
		// if the variable is empty the door name is used otherwise the old name is used
	RET
		dialog_string
	END
END

DEFINE_PATCH_FUNCTION CreateDoorScriptPatchAnimation
	STR_VAR
		animation_name   = ~~
		door_name        = ~~
		door_script		 = ~~
	RET
		door_script
BEGIN
	INNER_ACTION BEGIN
		COPY ~.../%MOD_FOLDER%/inlined_door_flame.baf~ ~%workspace%/bif/temp/%door_script%.baf~ EVAL
	END
END

DEFINE_PATCH_FUNCTION CreateTriggerScriptBggodest
	STR_VAR
		area_name_dest 	= ~~
		trigger_script	= ~~
	RET
		trigger_script
		area_name_dest
BEGIN
	INNER_ACTION BEGIN
		COPY ~.../%MOD_FOLDER%/inlined_bggodest.baf~ ~%workspace%/bif/temp/%trigger_script%.baf~ EVAL
//		COMPILE ~%workspace%/bif/temp/%trigger_script%.baf~ 
	END
END

DEFINE_PATCH_FUNCTION CreateHouseAreaScript
	STR_VAR
		area_name_house 	= ~~
		area_name_1 		= ~~
		area_name_2 		= ~~
BEGIN
	GET_OFFSET_ARRAY triggers ARE_V10_REGIONS
	PHP_EACH triggers AS i => off BEGIN
		READ_ASCII off triggername (32) NULL
		READ_ASCII off + 0x38 destination
		PATCH_IF ~%destination%~ STR_EQ ~%area_name_1%~ BEGIN
			SPRINT trigger_name_1 ~%triggername%~
		END
		PATCH_IF ~%destination%~ STR_EQ ~%area_name_2%~ BEGIN
			SPRINT trigger_name_2 ~%triggername%~
		END
	END

	PATCH_IF debug = 1 BEGIN	
		PATCH_PRINT ~trigger_name_1: %trigger_name_1%~
		PATCH_PRINT ~trigger_name_2: %trigger_name_2%~
		PATCH_PRINT ~%area_name_house%: %area_name_1% : %area_name_2%~
	END
	INNER_ACTION BEGIN
		ACTION_IF FILE_EXISTS_IN_GAME ~%are2%.bcs~ BEGIN
			EXTEND_BOTTOM ~%are2%.bcs~ ~.../%MOD_FOLDER%/inlinedAreaScriptTriggerActivation.baf~ EVAL
		END ELSE BEGIN
			COPY ~.../%MOD_FOLDER%/inlinedAreaScriptTriggerActivation.baf~ ~%workspace%/bif/temp/%are2%.baf~
			COMPILE EVAL ~%workspace%/bif/temp/%are2%.baf~
		END
	END
END
